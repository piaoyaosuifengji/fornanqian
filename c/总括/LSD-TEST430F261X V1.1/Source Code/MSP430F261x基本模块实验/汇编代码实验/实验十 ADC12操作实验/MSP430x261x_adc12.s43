;*******************************************************************************
; 版权:   杭州利尔达科技有限公司
; 文件名:MSP430F261x_ADC12.S43
; 版本：V1.0
; 工作环境:IAR Embedded Workbench Evaluation for MSP430 4.10A
; 作者:Huanglongsong
; 描述：基于MSP430F261x内部ADC12模块，从A1采集外部模拟量经转换后的值与一基值比较，
;    如果比基值大，LED点亮，反之熄灭！
;
;
;                MSP430F261x
;             -----------------
;         /|\|              XIN|-
;          | |                 | 32kHz
;          --|RST          XOUT|-
;            |                 |
;     Vin -->|P6.1/A1      P5.7|--> LED
;
;

;*******************************************************************************
#include "msp430x26x.h"
;-------------------------------------------------------------------------------
            RSEG    CSTACK                  ; 定义堆栈段
;-------------------------------------------------------------------------------
            RSEG    CODE                    ; 定义程序段
;-------------------------------------------------------------------------------
RESET       mov.w   #SFE(CSTACK),SP         ; 初始化堆栈指针
StopWDT     mov.w   #WDTPW+WDTHOLD,&WDTCTL  ; 停止开门狗
SetupADC12  mov.w   #SHT0_2+ADC12ON,&ADC12CTL0 ; 设置采样时间，开ADC12，Vref = VACC
            mov.w   #SHP,&ADC12CTL1         ; 使用定时器采样
            mov.b   #INCH_1,&ADC12MCTL0     ; 选用A1通道
            mov.w   #01h,&ADC12IE           ; 开ADC12MCTL0中断
            bis.w   #ENC,&ADC12CTL0         ; 启动转换
            bis.b   #BIT7,&P5DIR            ; P5.7输出-LED
                                            ;
Mainloop    bis.w   #ADC12SC,&ADC12CTL0     ; 软件启动转换
            bis.w   #CPUOFF+GIE,SR          ; LPM0模式，由ADC12中断唤醒
            jmp     Mainloop                ; 重复执行
                                            ;
;-------------------------------------------------------------------------------
ADC12_ISR;  Exit LPM0 on reti
;-------------------------------------------------------------------------------
            bic.b   #BIT7,&P5OUT            ; 位P5.7，LED灭
            cmp.w   #07FFh,&ADC12MEM0       ; ADC12MEM = A0 > 0.5AVcc?
            jlo     ADC12_ISR_1             ; 
            bis.b   #BIT7,&P5OUT            ; 置位P5.7，LED亮
ADC12_ISR_1 bic.w   #CPUOFF,0(SP)           ; 退出LMP0
            reti                            ;
                                            ;
;-------------------------------------------------------------------------------
            COMMON  INTVEC                  ; Interrupt Vectors
;-------------------------------------------------------------------------------
            ORG     ADC12_VECTOR            ; ADC12 中断入口
            DW      ADC12_ISR
            ORG     RESET_VECTOR            ; 系统上电复位
            DW      RESET
            END
