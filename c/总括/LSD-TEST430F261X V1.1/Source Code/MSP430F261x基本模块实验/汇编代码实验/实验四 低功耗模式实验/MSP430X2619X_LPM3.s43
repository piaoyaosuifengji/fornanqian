/*******************************************************************************
; 版权:    杭州利尔达科技有限公司
; 文件名:  MSP430F261x_lpm3.S43
; 版本：   V1.0
; 工作环境:IAR Embedded Workbench Evaluation for MSP430 4.11B
; 作者:    Huanglongsong 
; 描述：MSP430系列特有的低功耗试验例程，初始化完系统后进入低功耗模式三，由WDT定时
;       器定时4秒后唤醒CPU,执行一次LED闪烁，继续进入低功耗模式三，这样周而复始的
;       进行着。
;  ACLK = LFXT1/4 = 32768/4, MCLK = SMCLK = default DCO ~1.045MHz
; 生成日期:2008年12月
; 说明：1、LPM3状态下，时钟系统中只有XT1工作，为ACLK提供时钟源。
;       2、WDT定时器时钟选择ACLK，如果选择SMCLK，WDT将停止中做。
;
;        MSP430F261x
;         ---------------
;     /|\|            XIN|-
;      | |               | 32kHz
;      --|RST        XOUT|-
;        |               |
;        |           P5.7|-->LED
;
;
******************************************************************************/
#include "msp430x26x.h"
;-------------------------------------------------------------------------------
            RSEG    CSTACK                  ; 定义堆栈段
;-------------------------------------------------------------------------------
            RSEG    CODE                    ; 定义程序段
;-------------------------------------------------------------------------------
RESET       mov.w   #SFE(CSTACK),SP         ; 始化堆栈
SetupWDT    mov.w   #WDT_ADLY_1000,&WDTCTL  ; WDT 1s*4定时
SetupBC     bis.b   #DIVA_1,&BCSCTL1        ; ACLK/4
            bis.b   #WDTIE,&IE1             ; 能WDT中断
SetupP1     mov.b   #0FFh,&P1DIR            ; P1.x 输出
            clr.b   &P1OUT                  ; P1.x 输出0
SetupP2     mov.b   #0FFh,&P2DIR            ; P2.x 输出
            clr.b   &P2OUT                  ; P2.x 输出0
SetupP3     mov.b   #0FFh,&P3DIR            ; P3.x 输出
            clr.b   &P3OUT                  ; P3.x 输出0
SetupP4     mov.b   #0FFh,&P4DIR            ; P4.x 输出
            clr.b   &P4OUT                  ; P4.x 输出0
SetupP5     mov.b   #0FFh,&P5DIR            ; P5.x 输出
            clr.b   &P5OUT                  ; P5.x 输出0
                                            ;
Mainloop    bis.w   #LPM3+GIE,SR            ; 进入LPM3，打开中断使能
            bis.b   #BIT7,&P5OUT            ; P5.7输出1
            push.w  #20000                  ; 延时量载入堆椎
Delay       dec.w   0(SP)                   ; 延时量减1
            jnz     Delay                   ; 延时时间到了吗？
            incd.w  SP                      ; 恢复堆椎指针
            bic.b   #BIT7,&P5OUT            ; 复位P5.7
            jmp     Mainloop                ; 重复
                                            ;
;-------------------------------------------------------------------------------
WDT_ISR  ;  退出LPM3
;-------------------------------------------------------------------------------
            bic.w   #LPM3,0(SP)             ; 退出LPM3
            reti                            ;
                                            ;
;-------------------------------------------------------------------------------
            COMMON  INTVEC                  ; 中断向量定义
;-------------------------------------------------------------------------------
            ORG     WDT_VECTOR              ; WDT 中断
            DW      WDT_ISR
            ORG     RESET_VECTOR            ; 复位中断
            DW      RESET
            END
